template header
ruleName
attribute
comparator
valueToCompare
alarmName
alarmDescription
alarmType
numOfEvents
timeFrame

package drools.templates;

import bsep.sc.SiemCenter.events.LogEvent;
import bsep.sc.SiemCenter.events.Alarm;
import bsep.sc.SiemCenter.events.AlarmType;
import java.util.Date;

template "Multiple logs with single condition template"

rule "id:@{row.rowNumber} - @{ruleName}"
    no-loop true
    when
        $log1: LogEvent($src: machineSource)
        not ( Alarm(machineSource == $src && name == "@{alarmName}") )
        Number(intValue >= @{numOfEvents}) from accumulate(
            $log2: LogEvent(
                this != $log1,
                @{attribute} @{comparator} "@{valueToCompare}"
            ) over window:time(@{timeFrame}),
            count($log2)
        )
    then
        System.out.println("@{ruleName} - @{row.rowNumber}");
        insert(new Alarm($src, "@{alarmName}", "@{alarmDescription}", AlarmType.@{alarmType}, new Date()));
end

end template